import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.android.databinding'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'kotlin-android'
apply plugin: 'de.undercouch.download'

BuildProperties defaultProperties = BuildProperties.load('/build-props/base.properties', project)
BuildProperties.generateFlavors(new File('build-props').listFiles(), project)

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    archivesBaseName = 'kazak'

    defaultConfig {
        versionName defaultProperties.getString('versionName')
        minSdkVersion 19
        targetSdkVersion 23

        buildConfigField "String", "BUILD_TIME_FORMATTED", "\"${getBuildTimestamp()}\""
        project.ext.intermediateClasses = "$projectDir/build/intermediates/classes/debug/"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        example {
            defaultProperties.configure(it)
            project.ext.intermediateClasses = "$projectDir/build/intermediates/classes/example/debug/"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        lintConfig file("${project(':').projectDir}/config/lint.xml")
    }

    applicationVariants.all { variant ->
        def taskName = "download${variant.name.capitalize()}Schedule"
        project.task(taskName) << {
            def scheduleUrl = variant.getMergedFlavor().getBuildConfigFields().get("ENDPOINT_URL").getValue()
            scheduleUrl = scheduleUrl[1..scheduleUrl.length() - 2]
            def assetsDir = android.sourceSets[variant.flavorName].assets.srcDirs[0]
            download {
                src "${scheduleUrl}/events?expand=rooms&expand=presenters&expand=track&expand=rooms&expand=secondaryRooms"
                dest "${assetsDir}/json/schedule.json"
            }
        }
    }
}

dependencies {
    apt 'com.google.dagger:dagger-compiler:2.0.2'

    provided 'org.glassfish:javax.annotation:3.1.1-b02'

    provided 'org.jetbrains:annotations:15.0'

    compile project(':core')
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:preference-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.google.dagger:dagger:2.0.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.google.code.findbugs:annotations:3.0.0'
    compile ('com.facebook.stetho:stetho:1.2.0') {
        exclude (group: 'com.google.code.findbugs', module: 'jsr305')
    }
}

def getBuildTimestamp() {
    SimpleDateFormat dateFormat = new SimpleDateFormat("yyyy-MM-dd HH:mm:ss 'UTC'");
    dateFormat.setTimeZone(TimeZone.getTimeZone("UTC"));
    return dateFormat.format(new Date());
}

apply from: "${project(':').projectDir}/config/static_analysis.gradle"
