apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

check.dependsOn 'checkstyleCheck', 'pmdCheck', 'findbugsCheck'

def humanReadable = project.hasProperty('humanReadable')

task pmdCheck(type: Pmd) {
    ignoreFailures = true

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        html.enabled = humanReadable
    }
}

task checkstyleCheck(type: Checkstyle) {
    ignoreFailures = true
    configFile file("${project(':').projectDir}/config/checkstyle/checkstyle.xml")

    source 'src'
    include '**/*.java'
    classpath = files()
}

task checkstyleHtml << {
    ant.xslt(in: checkstyleCheck.reports.xml.destination,
            style: file("${project(':').projectDir}/config/checkstyle/checkstyle-noframes-sorted.xsl"),
            out: new File((String) checkstyleCheck.reports.xml.destination.parent, 'checkstyle.html'))
}

if (humanReadable) {
    checkstyleCheck.finalizedBy checkstyleHtml
}

task findbugsCheck(type: FindBugs, dependsOn: assembleDebug) {
    ignoreFailures = true
    classes = fileTree("${project.ext.intermediateClasses}")
    source = fileTree("$projectDir/src/main/java/")
    classpath = files()
    excludeFilter = file("${project(':').projectDir}/config/findbugs/excludeFilter.xml")
    effort = 'max'
    reports {
        // Findbugs doesn't unfortunately support more than one report at a time
        xml.enabled = !humanReadable
        html.enabled = humanReadable
    }
}


